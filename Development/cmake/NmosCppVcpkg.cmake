if(POLICY CMP0074)
    cmake_policy(SET CMP0074 NEW)
else()
    message(FATAL_ERROR "Policy CMP0074 not vailable. You need CMake 3.12 or newer to use Vcpkg to acquire dependencies!")
endif()
if(DEFINED WIN32) # unfortunately CMAKE_SYSTEM_NAME is not yet set here
    if (NOT DEFINED VCPKG_TARGET_TRIPLET)
        if (${CMAKE_GENERATOR_PLATFORM} STREQUAL "x64")
            if (${USE_STATIC_RUNTIME})
                set(VCPKG_TARGET_TRIPLET "x64-windows-static")
            else()
                set(VCPKG_TARGET_TRIPLET "x64-windows")
            endif()
        else()
            if (${USE_STATIC_RUNTIME})
                set(VCPKG_TARGET_TRIPLET "x86-windows-static")
            else()
                set(VCPKG_TARGET_TRIPLET "x86-windows")
            endif()
        endif()
    endif()
    message(STATUS "Using Vcpkg triplet ${VCPKG_TARGET_TRIPLET}")
endif()
if(DEFINED ENV{VCPKG_ROOT})
    if(DEFINED CMAKE_TOOLCHAIN_FILE)
        # need to be eqaul
        if(NOT ${CMAKE_TOOLCHAIN_FILE} STREQUAL "$ENV{VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake")
            message(FATAL_ERROR "USE_VCPKG switched on but also CMAKE_TOOLCHAIN_FILE set (CMAKE_TOOLCHAIN_FILE = ${CMAKE_TOOLCHAIN_FILE}, ENV{VCPKG_ROOT} = $ENV{VCPKG_ROOT})")
        endif()
    else()
        set(CMAKE_TOOLCHAIN_FILE "$ENV{VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake" CACHE STRING "")
    endif()
else()
    if(NOT DEFINED CMAKE_TOOLCHAIN_FILE)
        message(FATAL_ERROR "USE_VCPKG switched on but neither VCPKG_ROOT environment variable available nor CMAKE_TOOLCHAIN_FILE defined")
    endif()
endif()
message(STATUS "Using CMAKE_TOOLCHAIN_FILE = ${CMAKE_TOOLCHAIN_FILE}")
